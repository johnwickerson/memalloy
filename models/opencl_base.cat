(* This is a modified version of a file distributed with the paper
"Overhauling SC atomics in C11 and OpenCL" by Mark Batty,
Alastair F. Donaldson, and John Wickerson (POPL'16). The original 
is available online: http://multicore.doc.ic.ac.uk/overhauling/

The deadness predicates come from the paper "Automatically Comparing
Memory Consistency Models" by John Wickerson, Mark Batty,
Tyler Sorensen and George A. Constantinides (POPL'17).
 *)

"OpenCL"

include "basic.cat"
include "fr.cat"

(* workgroup-scoped is just a synonym for "atomic" *)
let WG = A
      
let fsb = [F]; po
let sbf = po; [F]

(* release sequence *)
let rs = poloc*; rf*

(* inclusive scopes (conservative original) *)
let incl' = swg & (WG * WG)
	  | sdv & (DV * DV)
	  |       (SY * SY)

(* inclusive scopes (less conservative version) *)
let incl1 = [WG]; swg
	  | [DV]; sdv
	  | [SY]; uni
let incl = incl1 & incl1^-1
	 | [REM]; incl1
	 | incl1^-1; [REM]
      
let scf = (SC * SC)
	| (G & L & F) * (G & L & F)

let swra_local =
  ([L & REL]; fsb?; [A & W]; rs; [L]; rf; [R & A]; sbf?; [L & ACQ])
  & incl \ thd

let swra_global =
  ([G & REL]; fsb?; [A & W]; rs; [G]; rf; [R & A]; sbf?; [G & ACQ])
  & incl \ thd
						  
let sw_local = swra_local | (scf & swra_global)

let sw_global = swra_global | (scf & swra_local)

let ghb = ((G * G) & po | sw_global)+
let lhb = ((L * L) & po | sw_local)+

let hb = ghb | lhb			

acyclic ghb as Ghb
acyclic lhb as Lhb
				  
irreflexive (co | co;rf | fr | fr;rf); ghb as Gcoh
irreflexive (co | co;rf | fr | fr;rf); lhb as Lcoh

irreflexive rf;hb as Rf

empty (rf;[NAL & G]) \ imm([W];(ghb & sloc)) as GnaRf
empty (rf;[NAL & L]) \ imm([W];(lhb & sloc)) as LnaRf

irreflexive rf | co;fr | co;rf as Rmw

let cnf = ((W * M) | (M * W)) & sloc

let dr = cnf \ (hb | hb^-1) \ thd \ incl
undefined_unless empty dr as Dr
				  
(* no "if(r==0)" *)
deadness_requires empty if_zero as No_If_Zero 

(* no unsequenced races *)
(* deadness_requires empty ur as Dead_Ur *)

(* coherence edges are forced *)
deadness_requires empty unforced_co as Forced_Co

(* external control dependency *)
let cde = ((rf \ thd) | ctrl)* ; ctrl
(* dependable release sequence *)
let drs = rs \ ([R]; !cde)
(* dependable synchronises-with *)
let dsw_global = sw_global & (((fsb?; [REL]; drs?) \ (ctrl^-1; !cde)) ; rf)
let dsw_local = sw_local & (((fsb?; [REL]; drs?) \ (ctrl^-1; !cde)) ; rf)
(* dependable happens-before *)
let dhb_global = po?; (dsw_global;ctrl)*
let dhb_local = po?; (dsw_local;ctrl)*
(* potential data race *)
let pdr = cnf \ incl

deadness_requires empty pdr \ (dhb_global | dhb_global^-1 | dhb_local | dhb_local^-1) as Dead_Pdr
